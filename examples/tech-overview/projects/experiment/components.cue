package holos

import ks "sigs.k8s.io/kustomize/api/types"

// Derive common labels and namespace from the project registration.
let Project = #Projects.experiment

// This is an example of how the platform team constrains the project
// components.  In this case the platform team ensures helm charts are managed
// in the project namespace and common labels are applied to all resources
// generated by the helm chart.
#Helm: {
	// Ensure the chart installs into the project namespace.
	Namespace: Project.Namespaces[Project.Name].Name

	// Necessary to ensure the resources go to the correct namespace.
	EnableKustomizePostProcessor: true
	KustomizeFiles: "kustomization.yaml": ks.#Kustomization & {
		// Ensure metadata.namespace field is added to all resources.
		namespace: Namespace
		// Add common labels to all resources.
		commonLabels: {
			"\(#Organization.Domain)/project.name": Project.Name
			"\(#Organization.Domain)/owner.name":   Project.Owner.Name
			"\(#Organization.Domain)/owner.email":  Project.Owner.Email
		}
	}
}
